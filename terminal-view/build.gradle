apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    namespace "com.termux.view"
    compileSdkVersion project.properties.compileSdkVersion.toInteger()

    dependencies {
        implementation "androidx.annotation:annotation:1.3.0"
        api project(":terminal-emulator")
    }

    defaultConfig {
        minSdkVersion project.properties.minSdkVersion.toInteger()
        targetSdkVersion project.properties.targetSdkVersion.toInteger()
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    testImplementation "junit:junit:4.13.2"
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                groupId = 'com.termux'
                artifactId = 'terminal-view'
                version = '0.118.0'
                // Only add component if it exists (for publishing)
                try {
                    from components.release
                } catch (Exception e) {
                    // Component not available, skip for now
                }
                artifact(sourceJar)
            }
        }
    }
}
