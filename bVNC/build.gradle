apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion compileApi

    defaultConfig {
        targetSdkVersion targetApi
        minSdkVersion project.getProperties().get("SDK_VERSION")
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    packaging {
        resources {
            excludes += ['lib/armeabi/libsqlcipher.so', 'lib/mips64/libsqlcipher.so', 'lib/mips/libsqlcipher.so', 'META-INF/versions/9/OSGI-INF/MANIFEST.MF']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    
    kotlinOptions {
        jvmTarget = '1.8'
    }
    
    sourceSets.main {
        // Exclude RDP-related files if FreeRDP is not available
        def freeRdpAvailable = findProject(':remoteClientLib:jni:libs:deps:FreeRDP:client:Android:Studio:freeRDPCore') != null
        if (!freeRdpAvailable) {
            java.exclude '**/protocol/RemoteRdpConnection.kt'
            java.exclude '**/RemoteRdpConnection.kt'
            java.exclude '**/input/RemoteRdpKeyboard.java'
            java.exclude '**/RemoteRdpKeyboard.java'
            java.exclude '**/input/RemoteRdpPointer.java'
            java.exclude '**/RemoteRdpPointer.java'
        }
    }
    
    namespace 'com.undatech.remoteClientUi'
}

// Conditionally exclude RDP-related files from compilation when FreeRDP is not available
def freeRdpAvailable = findProject(':remoteClientLib:jni:libs:deps:FreeRDP:client:Android:Studio:freeRDPCore') != null
if (!freeRdpAvailable) {
    afterEvaluate {
        // Exclude from Kotlin compilation
        tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
            source = source.filter { file ->
                !file.path.contains('RemoteRdpConnection.kt')
            }
        }
        // Exclude from Java compilation
        tasks.withType(JavaCompile).configureEach {
            source = source.filter { file ->
                !file.path.contains('RemoteRdpKeyboard.java') && 
                !file.path.contains('RemoteRdpPointer.java')
            }
        }
    }
}

dependencies {
    implementation project(':pubkeyGenerator')
    implementation project(':remoteClientLib')
    // freeRDPCore is conditionally included in settings.gradle if it exists
    if (findProject(':remoteClientLib:jni:libs:deps:FreeRDP:client:Android:Studio:freeRDPCore') != null) {
        implementation project(':remoteClientLib:jni:libs:deps:FreeRDP:client:Android:Studio:freeRDPCore')
    }
    implementation group: 'org.bouncycastle', name: 'bctls-jdk18on', version: '1.80'
    implementation group: 'androidx.appcompat', name: 'appcompat', version: '1.4.1'
    implementation group: 'androidx.legacy', name: 'legacy-support-v4', version: '1.0.0'
    implementation group: 'androidx.vectordrawable', name: 'vectordrawable', version: '1.1.0'
    implementation group: 'androidx.preference', name: 'preference-ktx', version: '1.2.0'
    // Use SQLCipher from Maven Central instead of local AAR
    implementation "net.zetetic:android-database-sqlcipher:4.5.4"
    implementation "androidx.sqlite:sqlite-ktx:2.2.0"
    implementation group: 'org.yaml', name: 'snakeyaml', version: '2.4'
    implementation 'org.apache.httpcomponents:httpcore:4.4.10'
    implementation "com.github.luben:zstd-jni:1.5.7-4@aar"
    implementation "androidx.core:core-ktx:1.7.0"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    // MultiDex support
    implementation 'androidx.multidex:multidex:2.0.1'
    // Trilead SSH library
    implementation 'com.trilead:trilead-ssh2:1.0.0-build217'

    implementation 'com.google.android.play:review:2.0.1'
    implementation 'com.google.android.play:review-ktx:2.0.1'
    implementation project(path: ':common')
    implementation 'com.google.android.gms:play-services-base:18.0.1'
}
repositories {
    mavenCentral()
}
